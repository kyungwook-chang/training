set colorsequence classic
set datafile separator ','

set terminal postscript eps font "Arial,25" size 5in,4.5in
#set terminal qt font "Arial,25"

set size ratio 0.8

set style line 1 linewidth 6 pointtype 5 pointsize 2.15
set style line 2 linewidth 6 pointtype 7 pointsize 2.5
set style line 3 linewidth 6 pointtype 9 pointsize 3.2
set style line 4 linewidth 6 pointtype 11 pointsize 3.2
set style line 5 linewidth 6 pointtype 13 pointsize 3.2
set style line 6 linewidth 6 pointtype 15 pointsize 3

set style line 101 linewidth 1.0 dashtype 1 linecolor rgb 'gray'
set style line 102 linewidth 1.0 dashtype 3 linecolor rgb 'gray'

#set logscale x
set xrange [0.4:1.3]
set xtics 0.2
set mxtics 2
#set logscale y
set yrange [0:22]
set ytics 4
set mytics 2

set grid xtics ytics mxtics linestyle 101, linestyle 101, linestyle 102

set key box reverse Left outside horizontal bottom center maxcols 2

#array label[6]
#label[1]="28nm C2D M3D"
#label[2]="28nm C2D F2F"
#label[3]="14/16nm C2D M3D"
#label[4]="14/16nm C2D F2F"
#label[5]="7nm C2D M3D"
#label[6]="7nm C2D F2F"

set xlabel "frequency (GHz)"
set ylabel "power saving (%)"

set output "test.eps"

N=`awk -F ',' 'NR==1 {print NF}' ./data/total_power.txt`
print N
#plot for [i=2:N] './data/total_power.txt' u 1:i with linespoints linestyle i-1 title label[i-1]
plot for [i=2:N] './data/total_power.txt' u 1:i with linespoints linestyle i-1 title columnhead

#set terminal qt font "Arial,25"
#set output
#replot
